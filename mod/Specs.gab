spec\t = spec:

explain_specs = (specs separator)
  => specs
      .transduce(
        ''
        +:
        Streams.map spec\name:
        |> Streams.interpose separator)

explain_values = (strings separator)
  => strings
      .transduce(
        ''
        +:
        Streams.map ((v) => Strings.make("\" v))
        |> Streams.interpose separator)

defspec: .def (
  Messages.t,
  (opts) => do
    spec\help:.def(
      opts.spec
      () => opts.at help:)

    spec\name:.def(
      opts.spec
      self.to\s)

    spec:.def(
      self
      opts.spec)

    opts.spec
  end)

[Messages.t] .defmodule {
  spec\doc:     () => (self.spec.help, self.spec\explain)
  spec\help:    () => self.spec.spec\help.unwrap
  spec\gen:     () => self.spec.spec\gen
  spec\explain: () => self.spec.spec\explain
  to\spec:     () => do
    spec = self
    spec: .at spec .then(() => spec.spec) .or spec
  end
}

to\spec: .def () => self

## PREDICATES
# A spec predicate is a function which
#   - Takes a value as input
#   - returns (true, spec, value, path) if the value fits the spec,
#      where spec is the spec that accepted the value
#      where value is the accepted value
#      where path is the 'path' taken to the accepted value ( or nil, if no path )
#   - returns (false, spec, value, path) if the value does not fit the spec,
#      where spec is the predicate that rejected the value ( or nil, if no spec was found )
#      where value is the rejected value
#      where path is the 'path' taken to the rejected value ( or nil, if no path )

# NIL PREDICATE
nil\predicate = predicate\nil:

spec\fits:.def(
  nil\predicate
  value => (value == nil: self value))

spec\gen:.def(
  nil\predicate
  nil:)

spec\explain:.def(nil\predicate 'nil')

# UNKNOWN PREDICATE
unknown\predicate = predicate\unknown:

unknown_values = [ nil: 0 1 'test' true: ]

[unknown\predicate] .defmodule {
  spec\fits: value => (true: self value)

  spec\gen: () => do
    idx = Numbers.int\between(0 unknown_values.len - 1)
    unknown_values.at idx .unwrap
  end

  spec\explain: 'unknown'
}

# INT PREDICATE
int\predicate = predicate\int:

spec\fits:.def(
  int\predicate
  value => (value.is\n, self, value))

spec\gen:.def(
  int\predicate
  () => Numbers.int\between(-10000 10000))

spec\explain:.def(int\predicate 'int')

#
# FLOAT PREDICATE
#

float\predicate = predicate\float:

spec\fits:.def(
  float\predicate
  (value) => (value.is\n, self, value))

spec\gen:.def(
  float\predicate
  () => Numbers.float\between(-10000, 10000))

spec\explain:.def(float\predicate 'float')

#
# STRING PREDICATE
#
string\predicate = predicate\string:

is\s:.def(Strings.t, true:)
is\s:.def false:

spec\fits:.def(
  string\predicate
  (value) => (value.is\s, self, value))

random\strings = [
  'vim-users'
  'emacs-users'
  'a'
  'b'
  'c'
]

spec\gen:.def(
  string\predicate
  () => do
    idx  = Numbers.int\between(0 ,random\strings.len)
    random\strings .at idx .unwrap
  end)

spec\explain:.def(string\predicate 'string')

#
# SHAPE PREDICATE
#
shape\predicate = predicate\shape:

spec\fits:.def(
  shape\predicate
  (value) => (value.is\shp, self, value))

spec\gen:.def(
  shape\predicate
  () => Shapes.make)

spec\explain:.def(shape\predicate 'shape')

#
# BOOL PREDICATE
#
boolean\predicate = predicate\boolean:

spec\fits:.def(
  boolean\predicate
 (value) =>(value.is\b, self, value))

spec\gen:.def(
  boolean\predicate
  () => Numbers.float\between() > 0.5)

spec\explain:.def(boolean\predicate 'boolean')

#
# COMPOSING SPECS
#
and\predicate = (specs*) => do
  (specs.len > 0).assert! 'Must have at least one spec for the and predicate'

  pred = Strings.make('predicate:and:' specs).to\m

  [pred] .defmodule {
    spec\explain: () => explain_specs.(specs ' & ')

    spec\fits: value => specs.reduce(
          []
          (acc  spec  key) => [spec.spec\fits(value) .or (false: spec value key)])*
    
    spec\gen: () => do
        start = specs .at 0 .unwrap

        xf = Streams.map(() => do
              value = start.spec\gen

              fits = specs.reduce(
                true:
                (a _ s) => a.then () => s .spec\fits value)

              (fits value)
            end)
            |> Streams.filter self:
            |> Streams.map first:
            |> Streams.take 1

        range:
          .make 100
          .transduce(0 first: xf)
          .assert!(Strings.make('Failed to generate a value for ' specs))
      end
  }

  # Return a predicate which returns true iff the given value
  # fits all of the given specs

  pred
end

or\predicate = (specs*) => do
  (specs.len > 0).assert "The or predicate expects more than zero specs"
  
  pred = Strings.make('predicate:any:' specs).to\m

  [pred] .defmodule {
    spec\fits: value => do
        valid = specs.reduce(false: (acc s) => do
          acc .else () => s .spec\fits value
        end)

        (valid self value pred)
      end

    spec\gen: () => specs.at(Numbers.int\between(0 specs.len - 1)).unwrap.spec\gen

    spec\explain: () => explain_specs.(specs ' | ')
  }
  # Return a predicate which returns true if the given value
  # fits any of the given specs

  pred
end

#
# COMPOSE WITH REGEX
#
regex\null  = { re\null: nil: meta: { name: 're.null' } path: [] }
regex\blank  = { re\blank: nil: meta: { name: 're.blank' } path: [] }
regex\empty = true:

re\null\t = regex\null?
re\blank\t = regex\blank?

re\null: .def(re\null\t, true:)
re\null: .def false:

re\blank:.def(re\blank\t, true:)
re\blank:.def false:

re\is\empty:.def(regex\empty true:)
re\is\empty:.def(re\blank\t, true:)
re\is\empty:.def false:

re\fields = [forms: meta: path:]

RE\Atom = re\atom: .defclass (
  re\atom: re\fields*
  default:
  {})

RE\Seq = re\seq: .defclass(
  re\seq: re\fields*
  default:
  {})

RE\Alt = re\alt: .defclass(
  re\alt: re\fields*
  default:
  {
    re\is\empty: () => self.forms.any re\is\empty:
  })

RE\Rep = re\rep: .defclass(
  re\rep: re\fields*
  default:
  {})

stringify_forms = (forms, sep)
  => Strings.make(
    "("
    forms.map re\debug: .join (Streams.interpose sep)
    ")")

collapse_path = (path) => do
  path.reduce({} (col elem) => do
    via = elem.at 0 .unwrap
    status = elem.at 1 .unwrap
    val = elem.at 2 .unwrap .at 1 .unwrap
    col.when(status put_via_by: v => v.cons val via*)
  end)
end

make: .defcase {
  RE\Atom (spec meta path) => RE\Atom.t.make(
    nil:
    spec
    meta
    path  .or [])

  RE\Seq (forms meta path) => do
    path = path .or [] + forms.transduce([] +: Streams.filter re\blank: |> Streams.map path:)
    forms = forms.skip re\blank:

    forms .cond {
      f => f.any   re\null:      regex\null
                                    .put(path: path + forms.transduce([] +: Streams.filter re\null: |> Streams.map path:))
      f => f.every re\is\empty:  regex\blank
                                    .put(path: path .or [])

      f =>((f.len == 1)
        .and(meta.name == '%'))      () => forms
                                    .at 0
                                    .unwrap
                                    .put(path: path)
                                    #.put_by(path: x => x + path .or [])

      else:                      RE\Seq.t.make(
                                    nil:
                                    forms
                                    meta
                                    path  .or [])
    }
  end

  RE\Alt (forms meta path) => do
    nonnull = forms.skip re\null:

    (_, p) = forms.map re\path: .sort len: .pop
    p = p.or []

    nonnull .cond {
      is\empty:                    regex\null
                                    .put(path: p .or path .or [])

      f => f .every re\is\empty:   nonnull.map re\is\empty: .at 0 .first

      f =>((f.len == 1)
        .and(meta.name == '%'))      () => nonnull
                                    .at 0
                                    .unwrap
                                    #.put(path: path)
                                    .put_by(path: x => x + path .or [])

      else:                        RE\Alt.t.make(
                                      nil:
                                      nonnull
                                      meta
                                      path  .or [])
    }
  end

  RE\Rep (form meta path) => form .cond {
        re\null:      regex\blank
                          .put(path: path .or [])
        re\is\empty:  regex\blank
                          .put(path: path .or [])
        re\blank:     form 
                     
        else:         RE\Rep.t.make(
                          nil:
                          form
                          meta
                          path  .or [])
    }
}

# Useful message for getting string representations of RE
re\debug: .defcase {
  regex\empty "re.internal.empty"
  re\blank\t "re.internal.blank"
  re\null\t "re.internal.null"

  RE\Atom.t
  () => self.re\name

  RE\Seq.t
  () => stringify_forms.(self.forms, " > ")

  RE\Alt.t
  () => stringify_forms.(self.forms, " | ")

  RE\Rep.t
  () => Strings.make('*', self.forms.re\name)
}
re\debug: .def () => self.to\spec.spec\name

# Get the user-supplied name of a RE
re\name: .defcase {
  RE\Atom.t
  () => self.meta.name

  RE\Seq.t
  () =>  self.meta.name

  RE\Alt.t
  () => self.meta.name

  RE\Rep.t
  () => self.meta.name
}
re\name: .def () => self.to\spec.spec\name

# Construct a path (array of triples) taken to *get to* this RE state
re\path: .defcase {
  regex\empty []
  re\blank\t () => self.path
  re\null\t () => self.path
  RE\Atom.t
  () => self.path
  # Combine the paths of all forms
  RE\Seq.t
  () => self.path + self.forms.transduce([] +: Streams.map re\path:)
  # Should probably select longest/shortest here
  RE\Alt.t
  () => self.path + self.forms.map re\path: .sort len: .at 0 .unwrap
  # Path is saved in self, maybe add forms path somewhere?
  RE\Rep.t
  () => self.path + self.forms.re\path
}

# Construct a RE representing the remaining
# RE to fulfill
re\remainder: .defcase {
  re\blank\t () => []
  re\null\t () => []
  RE\Atom.t
  () => []
  RE\Rep.t
  () => []
  # Probably take the longest/shortest remainder here
  RE\Alt.t
  () => self
    .forms
    .at 0 
    .unwrap
    .re\remainder
  # Skip any empty beginnings, return rest of regex
  RE\Seq.t
  () => RE\Seq.make(
    self
      .forms
      .collect(Streams.drop_while re\is\empty:),
    self.meta
    self.path)
}

re\seq\deriveforms: .defcase {
  Shapes.make(0) (re e steps) => do
    #'Applied seq $ to $\n'.printf(re.to\s, e)
    steps = steps.cons re

    form_re = self.at 0 .unwrap

    # Pass off to single form child, adding self as a step.
    re\d = form_re.re\d(e steps)

    # For some reason don't include re\d path?
    result = RE\Seq.make([re\d], re.meta, re.path)

    #'Result: $\n'.printf(result.to\s)
    result
  end
}

# In all other cases, we use the first two and do the work
re\seq\deriveforms: .def (re e steps) => do
    #'Applied seq $ to $\n'.printf(re.to\s, e)
    steps = steps.cons re

    (ok, a) = self.at 0
    (a, rest) = (self.at 0 .unwrap, self.slice(1, self.len))
    b = self.at 1 .unwrap

    (new_a) = a.re\d(e, steps)
    (new_b) = b.re\d(e, steps)

    (no_a) = a.re\empty
    (no_b) = b.re\empty

    so_far = re.path

    left = RE\Seq.make(
      self.put(0 new_a)
      re.meta)

    right = RE\Seq.make(
      rest.put(0 new_b)
      re.meta
      a.re\path) 

    altforms = [
      left
      no_a.re\is\empty
        .and right
        .or(regex\null.put(path: right.path))
      ]

    #'Altforms: $\n'.printf(altforms.to\s)

    result = RE\Alt.make(
      altforms
      { name: '%' },
      so_far)
      
    #'Result: $\n'.printf(result.to\s)
    result
end

# Derive a RE, given an element e.
# Basically, this *steps* the regex one element, producing
# a *new* regex which represents the remainder of the computation.
# The steps argument keeps track of the callstack of elements *within*
# the regex to eventually reach a re\atom that either fails/matches.
# The atom stores the steps in its path, which is passed back up to parents.
re\d: .defcase {
  re\null\t   e => self
  re\blank\t  e => regex\null.put(path: self.path)
  regex\empty e => regex\null.put(path: self.path)

  RE\Atom.t
  (e, steps) => do
    (ok, rest*) = self.forms.to\spec.spec\fits e

    #'Applied atom $ to $ -> $\n\n'.printf(self.to\s, e, ok)

    so_far = self.path

    steps = steps.cons self
    name = steps.collect(Streams.map re\name:
                  |> Streams.filter(x => x == '%' !))

    path = so_far.push [name, ok, rest]

    result = ok
      .and regex\blank
      .or regex\null
      .put(path: path)

    #'Result: $\n'.printf(result.to\s)
    result
  end

 RE\Alt.t
 (e steps) => do
    #'Applied alt $ to $\n'.printf(self.to\s, e)

    steps = steps.cons self
    forms = self.forms.map f => f .re\d(e steps)

    result = RE\Alt.make(
      forms
      self.meta
      self.path)

    #'Result: $\n'.printf(result.to\s)
    result
  end

  RE\Seq.t
  (e steps) => do
    self.forms.re\seq\deriveforms(self e steps)
  end

 RE\Rep.t
 (e steps) => do
    #'Applied rep $ to $\n'.printf(self.to\s, e)
    a = self.forms

    new_a = a.re\d(e steps.cons self)

    new_a_empty = new_a.re\empty.re\is\empty

    result = RE\Seq.make(
      [new_a self]
      { name: '%' }
      self.path)

    #'Result: $\n'.printf(result.to\s)
    result
  end
}

# Types that can match up these RE
# need to implement the head/tail interface.
# This is how the algorithm pops off an
# 'element' to process.
re\head: .defcase {
  Records.t, () => self.at 0 .unwrap
  Strings.t, () => self.at 0
}

re\tail: .defcase {
  Records.t, () => self.slice(1 self.len)
  Strings.t, () => self.slice(1 self.len)
}

# Used in conjunction with re\is\empty
# To determine if a RE is 'empty' (satisfied)
# Here, it more means the verb 'to empty', because
# we produce an 'emptied' RE as a result.
#re\empty: .def () => (regex\null, [])
re\empty: .defcase {
  re\null\t   () => (self)
  re\blank\t  () => (self)
  regex\empty () => (regex\blank)
  RE\Rep.t
  () => (regex\empty)
  RE\Atom.t
  () => (regex\null.put(path: self.path))
  RE\Seq.t
  () => do
    forms = self.forms.map re\empty:
    (RE\Seq.make(forms, self.meta, self.path))
  end
  RE\Alt.t
  () => do
    forms = self.forms.map re\empty:

    (RE\Alt.make(forms, self.meta, self.path))
  end
}

# The recursive match algo
# which drives RE matching.
match: .defcase {
# Naturally this only succeeds if the match is empty
# But what if one of the alt paths is empty
  []?, (re) => do
    path = re.re\path
    (re.re\empty.re\is\empty, collapse_path.(path), re.re\remainder.re\debug)
  end

  Records.t, (re) => do
    dre = re.re\d(self.re\head)

    self.re\tail.match dre
  end

  true: (tail ddc re) => do
    (re, path) = ddc .re\empty

    (re.re\is\empty, path)
  end

  false: (tail dre re) => do
    tail.match dre
  end

  Strings.t, re => do
    dre = re.re\d(self.re\head, "")

    tail = self.re\tail

    tail.len .== 0 .match (tail dre re)
  end
}

to\spec\re: .def () => RE\Atom.make(
  self
  { name: self.to\spec.spec\name })

pat: .def(
  spec\t
  (specs**) => do
    pred = Strings.make('predicate:pat:' specs).to\m

    forms = specs
      .map (v k) => do
        v.to\spec
         .to\spec\re
         .put_via(k meta: name:)
      end

    re = RE\Seq.make(forms {name: "pat"})

    spec\fits:.def(
      pred
      (value) => value .match re)

    generator = Streams.map to\spec:
             |> Streams.map spec\gen:
             |> Streams.map((args*) => Strings.make(args*))

    spec\gen:.def(
      pred
      () => specs.collect generator .join)

    to\spec\re: .def(pred re)

    pred
  end)

cat: .def(
  spec\t
  (specs**) => do
    pred = Strings.make('predicate:cat:' specs).to\m

    forms = specs.map (v k)
      => v
        .to\spec
        .to\spec\re
        .put_via(k meta: name:)

    re = RE\Seq.make(forms { name: "cat" })

    spec\fits: .def (
      pred
      (values*) => do
        values .match re
      end)

    generator = Streams.map to\spec:
             |> Streams.map spec\gen:

    spec\gen:.def(
      pred
      () => specs.collect generator *)

    to\spec\re: .def(pred re)

    pred
  end)

alt: .def(
  spec\t
  (specs**) => do
    (first rest*) = specs*
    pred = Strings.make('predicate:alt:' specs).to\m

    forms = specs.map (v k)
      => (v.to\spec.to\spec\re.put_via(k meta: name:))

    re = RE\Alt.make(forms { name: "alt" })

    spec\fits:.def(
      pred
      (values*) => do
        values .match re
      end)

    spec\gen:.def(
      pred
      () => do
        specs.at(Numbers.int\between(0 specs.len)).unwrap.spec\gen
      end)

    to\spec\re: .def(pred re)

    pred
  end)

*: .def(
  spec\t
  (spec) => do
    pred = Strings.make('predicate:*:' {} ':' spec).to\m

    re = RE\Rep.make(spec.to\spec.to\spec\re, { name: "*" })

    spec\fits: .def(
      pred
      (values*) => values .match re)

    generator = Streams.map(_ => spec.to\spec.spec\gen)

    spec\gen:.def(
      pred
      () => Ranges
              .make(0 Numbers.int\between(0 10))
              .collect generator
              .*)

    to\spec\re: .def(pred re)

    pred
  end)

+: .def(
  spec\t
  (spec) => do
    pred = Strings.make('predicate:+:' {} ':' spec).to\m

    re = spec.to\spec.to\spec\re

    re = RE\Seq.make([re, RE\Rep.make(re, { name: "rep+" })], { name: "seq+" })

    spec\fits:.def(
      pred
      (values*) => values .match re)

    spec\gen:.def(
      pred
      () => do
        Ranges
          .make(0 Numbers.int\between(1 10))
          .collect(Streams.map _ => spec.spec\gen
          .*)
      end)

    to\spec\re: .def(pred re)

    pred
  end)

slice\predicate = (slice) => do
  protocol = self
  keys = protocol.spec\protocol\keys

  pred = Strings.make('predicate:protocol:slice:' keys ':' slice).to\m

  spec\fits:.def(
    pred
    value => do
      keys.reduce([true: self value] (acc m) => do
# Check for a spec which corresponds to this message in the protocol
        acc.at 0 .unwrap.and acc .else () => do
          m .at value .then () => do
            m.to\m.spec.ok.then s => do
              s.spec\fits(value .at m .unwrap)
            end
          end .or [false:, self, value, m]
        end
      end).*
    end)

  genit = Streams.map (v) => do
    found = v.to\m.spec
    (v found.spec\gen)
  end

  spec\gen:.def(
    pred
    () => keys.transduce(protocol put: genit))

  spec\explain:.def(
    pred
    () => protocol.spec\exlain)

  pred
end


protocol\predicate = (protocol) => do
  pred = Strings.make('predicate:protocol:' protocol).to\m

  [pred] .defmodule {
    spec\explain: () => (protocol.len == 0)
        .and "[]"
        .or ("[ " + explain_values.(protocol, ', ') + " ]")

    spec\protocol\keys: [protocol*]

    spec\slice: slice\predicate
  }

  pred
end

list\predicate = spec => do
  pred = Strings.make('predicate:list:' spec).to\m

  [pred] .defmodule {
    spec\fits: value => value.spec\fits_each values:
    spec\gen: () => [spec.spec\gen]
    spec\explain: () => Strings.make('list[' spec.spec\explain() ']')
  }

  pred
end

record\predicate = (k_spec v_spec) => do
  pred = Strings.make('predicate:map:' k_spec ':' v_spec).to\m

  spec\fits:.def(
    pred
    value => do
      (value? == Records.t)
        .then(() => (false: self value))
        .else(() => do
          value .reduce(
            (v k) => do
              [ [k_spec, k], [v_spec, v] ] .spec\recursively
            end,
            )

          (true:, self, value)
        end)
    end)

  spec\gen:.def(
    pred
    () => { k_spec.spec\gen,  v_spec.spec\gen })

  spec\explain:.def(
    pred
    () => Strings.make('Map[' k_spec.spec\explain ', ' v_spec.spec\explain ']'))

  pred
end

spec\recursively: .defcase {
  # In the boolean cases, recurse if the spec matched
  # and bail early if it didn't
  true: (tail curr alt) => do
    tail.spec\recursively(alt*)
  end
  false: (tail curr alt) => do
    (self curr*)
  end
  # In the recursion cases, continue to check head 
  # until there is no tail
  nil: (alt*) => do
    alt*
  end
  'gab\\record' (alt*) => do
    (spec value) = self.head*

    (does_fit result*) = spec.spec\fits value

    does_fit .spec\recursively (self.tail, result alt)
  end
}

spec\block\Args = Shapes.make(input: output: semantics:)

spec\block\Application = Shapes.make(input: output: semantics: in: out:)

spec\block\input_fits:.def(
  spec\block\Application
  () => self.input.spec\fits(self.in*))

spec\block\output_fits:.def(
  spec\block\Application
  () => self.output.spec\fits(self.out*))

spec\semantics_fits:.def(
  nil:
  (input output) => (true: nil: [input output]))

spec\semantics_fits:.def(
  'gab\\block'
  (input output) => (self.(input output), self, [input output], semantics:))

block\predicate = (args semantics) => do
  args.assert\is spec\block\Args

  pred = Strings.make('predicate:block:' args ':' semantics).to\m

  spec\fits:.def(
    pred
    (input output) => do
      v = args
        .put(input_value: input)
        .put(output_value: output)

      v.first(
        apply:,
        [
          spec\block\input_fits:,
          spec\block\output_fits:,
          spec\semantic_fits:,
        ],
        to\option:,
      )
    end)

  spec\gen:.def(
    pred
    () => do
      block\gen = input* => do
        range:.make 100 .transduce(0, () => do
          # Generate an output
          output* = args.output.spec\gen

          v = args
            .put(input_value: input)
            .put(output_value: output)

          v.first(
            apply:,
            [
              spec\block\input_fits:,
              spec\block\output_fits:,
              spec\semantic_fits:,
            ],
            to\option:,
          )
        end).unwrap!
      end
    end)

  spec\check:.def(
    pred
    (block) => do
      range:.make 100 .transduce(() => do
        (input*) = args.input.spec\gen

        args.input.spec\fits(input*) .then do
          output* = block.(input*)
          
          (some:, pred.(input, output))
        end
      end)
    end)
  
  pred
end

general_message\predicate = 'predicate:message' .to\m

spec\fits:.def(
  general_message\predicate
  (value) => (value.is\m, self, value))

spec\gen:.def(
  general_message\predicate
  +:)

spec\explain:.def(
  general_message\predicate
  'message')

spec\message\input_fits:.def(
  'gab\\record'
  () => self.input.spec\fits(self.in*))

spec\message\output_fits:.def(
  'gab\\record'
  () => self.output.spec\fits(self.out*))

spec\message\receiver_fits:.def(
  'gab\\record'
  () => self.receiver.spec\fits(self.rec*))

spec\message\semantics_fits:.def(
  'gab\\record'
  () => self
    .at semantics:
    .unwrap ()
    .spec.semantics_fits(self.in, self.out))

dospec\apply: .defcase {
  true: (v acc f) => [f . v]
  false: (v acc f) => acc
}

-!>: .def (fs*) => do
  me = self # I'd like doing this to be a little neater
  fs.reduce([true:]
    (so_far f) => so_far
      .at 0 .unwrap
      .dospec\apply(me so_far f))*
end

spec\message\define:.defcase {
  nil: general_message\predicate
  'gab\\record' () => do
    args = self
    pred = Strings.make('predicate:message:' args).to\m

    spec\fits:.def(
      pred
      (rec in out)
        => args
          .put(rec: rec)
          .put(in: in)
          .put(out: out)
           -!>(spec\message\receiver_fits:
               spec\message\input_fits:
               spec\message\output_fits:
               spec\message\semantics_fits:))

    spec\gen:.def(
      pred
      receiver => do
        args.message.def(
          receiver
          (input*) => do
            (ok pred value path) = args.receiver.spec\fits self

            (ok pred value path) = ok
              .or (ok pred value path)
              .then () => args.input.spec\fits(input*)

            ok
              .or (ok pred value path)
              .then () => do
                (ok pred value path) = args.input.spec\fits(input*)
                (output*) = args.output.spec\gen
                iter = Streams.map(() => do
                    (output*) = args.output.spec\gen

                    args.output.spec\fits(output*) .then () => do
                      # If there are no semantics to satisfy, just return this output
                      (args.at semantics: .else () => output*) .then () => do
                        rec = args.receiver.spec\gen

                        args.receiver.spec\fits(rec) .then () => do

                          # Otherwise, we need to continue generating until we
                          # find a receiver and output which satisfy the semantics.
                          args.semantics.(input, output) .then () => do
                            # Return the generated output
                            (ok: output*)
                          end
                        end
                      end
                    end
                  end)            
                  |> Streams.filter ok:
                  |> Streams.map unwrap:
                  |> Streams.take 1

                range: .make 100 .transduce (false: first: iter)
          end
        end)

        args.message
      end)

    spec\check:.def(
      pred
      () => do
        xf = Streams.map(() => do
            receiver = args.receiver.spec\gen

            args.receiver.spec\fits(receiver) .then () => do
              (input*) = args.input.spec\gen

              args.input.spec\fits(input*) .then () => do
                (output*) = args.message.(receiver input*)

                (ok res*) = args.output.spec\fits(output*)
                
                 ok.then () => do
                  args.at semantics:
                    .or (ok, res*)
                    .then () => do
                      args.semantics.(input output)
                        .and(ok, res*)
                        .or(false:, args.semantics, [input output], 'semantics')
                    end
                end
              end
            end
          end)
          |> Streams.filter((valid spec value path) => valid!)
          |> Streams.map((valid spec value path) => option:.make(valid!, spec value path))
          |> Streams.take 1

        range:.make 100 .transduce([none:] all: xf)*
      end)

    spec\explain:.def(
      pred
      () => Strings.make(
        args.receiver.spec\name
        '.'
        args.message
        ' '
        args.input.spec\name
        ' => '
        args.output.spec\name))

    pred
  end
}

message\predicate = args => args.spec\message\define

LabeledSpec = Shapes.make(spec\label: spec\spec:)

spec\labeled: .def label => {
  spec\label: label
  spec\spec: self
}

[LabeledSpec] .defmodule {
  spec\explain: () => Strings.make(
      self.spec\label
      ': '
      self.spec\spec.spec\explain)

  spec\name: () => do
    Strings.make(
      self.spec\label
      ': '
      self.spec\spec.spec\name)
  end

  spec\gen:   () => self.spec\spec.spec\gen

  spec\fits: () => self.spec\spec.spec\fits

  spec\check: () => self.spec\spec.spec\check
}

spec\name: .def () => self.spec\explain
spec\explain: .def () => self
spec\gen: .def () => self
spec\fits: .def value => (self == value self value)

# Since a spec is just a predicate, we can simply call it and return the result
spec\fits: .def(
  'gab\\block'
  (value) => self.(value))

spec\fits: .def(
  'gab\\message'
  (value) => do
    msg = self
    msg .at value .then () => msg.(value)
  end)

to\spec\option:.defcase {
  nil: err => do
    spec\t.any(ok:, err.or err:)    
  end
}

to\spec\option: .def err => do
  value = self
  spec\t.any(
    spec\t.values(ok:, value),
    err.or err:,
  )
end

[spec\t] .defmodule {
  nil:      nil\predicate,
  unknown:  unknown\predicate,
  int:      int\predicate,
  float:    float\predicate,
  string:   string\predicate,
  shape:    shape\predicate,
  boolean:  boolean\predicate,
  block:    block\predicate,
  message:  message\predicate,
  list:     list\predicate,
  record:   record\predicate,
  protocol: protocol\predicate,
  all:      and\predicate,
  any:      or\predicate,
  option:   value => spec\t.any(
                      spec\t.values(ok:, value),
                      none:)
  result:  (value err) => value.to\spec\option err
  gt: (min) => do
    gt\predicate = Strings.make('predicate:gt:' min).to\m

    spec\fits:.def(
      gt\predicate
      (value) => (value > min, self, value))

    spec\explain:.def(gt\predicate, '> {min}')

    gt\predicate
  end
  lt: max => do
    lt\predicate = Strings.make('predicate:lt:' max).to\m

    spec\fits:.def(
      lt\predicate
      (value) => (value < max, self, value))

    spec\explain:.def([lt\predicate] '< {max}')

    lt\predicate
  end

  doc: (mod) => do
    xf = Streams.map ((v k) => (
          v
          k .to\s
            .split '\\'
            .to\m,
          k))
      |> Streams.filter ((_ k) => k == mod)
      |> Streams.map ((v m k) => do
        (h e) = k.spec\doc!
        Strings.make(k '\n' h '\n\t' e)
      end)
      |> Streams.interpose '\n\n'

    Messages
      .specializations
      .spec
      .transduce("" +: xf)
  end
}

spec\t
