'cgab'.use
ui = 'cgui'.use

(events, app) = (Channels.make, Channels.make)

#Fibers.make () => ui.run\tui(events, app).unwrap
Fibers.make () => ui.run\gui(events, app).unwrap

model = {
  input: ''
  history_index: 0
  history: []
}

red = '0xff0000ff'.as\n
blue = '0x0000ffff'.as\n
green = '0x00ff00ff'.as\n

HistoryElement = repl\history\elem: .defclass (
  source: fiber:
  default:
  {
    view: () => do
      fiber = self.fiber.to\s plain:
      [
        box:
        { width\grow: 1 border: { width: 4 color: red } padding: 8 }
        [
          [
            text: { content: self.source }
          ]
          [
            text: { content: '=> $'.sprintf(fiber) }
          ]
        ]
      ]
    end
  })

handle\keydown: .defcase {
  backspace: (ev model) => do
    model.put_by(input: pop:)
  end
  escape: (ev model) => do
    events.close
    app.close

    model
  end
  enter: (ev model) => do
    source = model.input

    (ok, fib) = source.as\gab

    history_elem = HistoryElement.make(source, fib)

    model = model
      .when(
        ok.ok?
        put: history: model.history.push history_elem)
      .put(input: "")

    model.put(history_index: model.history.len)
  end
  Messages.t
  (ev model) => do
    str = self.to\s
    model.put_by(input: curr => curr + str)
  end
}

handle\keyup: .defcase {
  Messages.t
  (ev model) => model
}

handle\key: .defcase {
  true: (msg ev model) => msg.handle\keydown(ev model)
  false: (msg ev model) => msg.handle\keyup(ev model)
}

handle\type: .defcase {
  tick:  (ev model) => do
    # Tick time
    model
  end
  mouse: (ev model) => do
    model
  end
  key:   (ev model) => do
    keymsg = ev.at 0 .unwrap
    down = ev.at 2 .unwrap
    down.handle\key(keymsg ev model)
  end
}

render\history: .def (
  model?
  () => self.history.map view:)

controller: .def (
  model?
  (type ev*) => do
    '$  $\n'.printf(type, ev)
    type
      .handle\type(ev self)
  end)

view: .def (
  model?
  () => do
    history = self.render\history
    [
      [
        box:
        { width\relative: 100 padding: 32 }
        history
      ]
      [
        box:
        {
          width\relative: 100
          height\relative: 10
          border: { width: 4 }
          padding: 32
        }
        [
          [text: { size: 32 content: self.input }]
        ]
      ]
    ]
  end)

events
  .pipe(
    app,
    Streams.reduce(model controller:)
    |> Streams.map view:
  )
